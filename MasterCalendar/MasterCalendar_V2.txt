TempMinMax:
LOAD
	Min(OrderDate) as MinDate,
	Max(OrderDate) as MaxDate
RESIDENT Orders;

LET vMinDate = Peek('MinDate', 0, 'TempMinMax'); 
LET vMaxDate = Peek('MaxDate', 0, 'TempMinMax'); 
LET vToday = $(vMaxDate);

//*************** Temporary Calendar ***************
TempCal:
LOAD 
	date($(vMinDate) + rowno() - 1) AS TempDate 
AUTOGENERATE 
	$(vMaxDate) - $(vMinDate) + 1; 
	
DROP TABLE TempMinMax;

//Create the Master Calendar
MasterCalendar:
LOAD	
    TempDate AS OrderDate,
    Week(TempDate) as Week,
//     Year(TempDate) as Year,
    Month(TempDate) as Month,
    Day(TempDate) as Day,
    WeekDay(TempDate) as WeekDay,
//     'Q' & Ceil(Month(TempDate) / 3) as Quarter,
	Date(monthstart(TempDate), 'MMM-YYYY') AS MonthYear,
//	WeekName(TempDate) AS WeekYear,
    Week(TempDate) &'-'& If(Week(TempDate)=1 AND
        Month(TempDate)=12,Year(TempDate)+1,
            If(Match( Week(TempDate), 52, 53) AND
                Month(TempDate)=1, Year(TempDate)-1,
                    Year(TempDate))) AS WeekYear,
    InYearToDate(TempDate, $(vToday), 0) * -1 as CYTDFlag,
    InYearToDate(TempDate, $(vToday), -1) * -1 as LYTDFlag,
    If(DayNumberOfYear(TempDate) <= DayNumberOfYear($(vToday)), 1, 0 ) as IsInYTD,
   	If(DayNumberOfQuarter(TempDate) <= DayNumberOfQuarter($(vToday)), 1, 0) as IsInQTD,
    If(Day(TempDate) <= Day($(vToday)), 1, 0) as IsInMTD,
    If(Month(TempDate) = Month($(vToday)), 1, 0) as IsCurrentMonth,
    If(Month(AddMonths(TempDate,1)) = Month($(vToday)), 1, 0) as IsLastMonth

RESIDENT TempCal 
ORDER BY TempDate ASC;

DROP TABLE TempCal;
